/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.7/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
//apply plugin: 'java-library'


allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    group = 'de.webtwob.agd.project'
    version = '0.0.1'
}

import org.gradle.plugins.ide.eclipse.model.SourceFolder

subprojects {

    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java-library'

    // In this section you declare where to find the dependencies of your project
    repositories {
        // Use jcenter for resolving your dependencies.
        // You can declare any Maven/Ivy/file repository here.
        jcenter()
        //mavenCentral for resolving dependencies
        mavenCentral()
    }

    sourceCompatibility = 10

    //eclipse should download javadoc and source files
    //and put everything on the module path
    eclipse {

        classpath {
            downloadJavadoc = true
            downloadSources = true
            file {
                whenMerged {
                    entries.findAll { it instanceof SourceFolder }*.each {
                        it.output = "bin/" + it.path.split("/")[2]
                    }
                    entries.findAll {
                        it.kind == 'src' || it.kind == 'lib'
                    }.each { it.entryAttributes['module'] = 'true' }
                }
            }
        }
    }

    //add not yet modularised jars to the module-path to form automatic-modules
    tasks.withType(JavaCompile) {
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    //idea should download javadoc and source files
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }

    }

}